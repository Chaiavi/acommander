buildscript {
    ext {
        appVersion = '2.5'
        shadowPluginVersion = '9.3.1'
        launch4jPluginVersion = '4.0.0'

        javafxVersion = '21.0.7'
        lombokVersion = '1.18.42'
        logbackVersion = '1.5.29'
        slf4jVersion = '2.0.17'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.gradleup.shadow:shadow-gradle-plugin:${shadowPluginVersion}"
        classpath "edu.sc.seis.launch4j:launch4j:${launch4jPluginVersion}"
    }
}

apply plugin: 'application'
apply plugin: 'com.gradleup.shadow'
apply plugin: 'edu.sc.seis.launch4j'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.2"
    implementation "org.openjfx:javafx-base:${javafxVersion}:win"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:win"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:win"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:win"
}

application {
    mainClass = 'org.chaiware.acommander.Launcher'
}

shadowJar {
    archiveBaseName = 'acommander'
    archiveClassifier = ''
    archiveVersion = ''
}

// Disable the problematic distribution tasks
tasks.named('shadowDistZip') { enabled = false }
tasks.named('shadowDistTar') { enabled = false }
tasks.named('startShadowScripts') { enabled = false }

// Configure clean task to also delete dist folder and ZIP file
tasks.named('clean') {
    delete 'dist'
    delete fileTree('.') {
        include 'acommander-v*.zip'
    }
}

// Task to copy config and apps folders to build/libs
def copyResources = tasks.register('copyResources', Copy) {
    from('config') {
        into 'config'
    }
    from('apps') {
        into 'apps'
    }
    into layout.buildDirectory.dir('libs')
}

// Launch4j configuration - simplified
launch4j {
    mainClassName = 'org.chaiware.acommander.Launcher'
    classpath = ['acommander.jar']
    bundledJrePath = 'runtime'
    icon = "${projectDir}/acommander.ico"
    outfile = 'acommander.exe'
    jarTask = project.tasks.shadowJar
    headerType = "gui"
    copyConfigurable = []

    // Version info using the variable
    version = appVersion
    textVersion = appVersion
    copyright = 'Chaiware.org'
    companyName = 'Chaiware.org'
    fileDescription = 'ACommander - NC Clone'
    productName = 'ACommander'

    // JVM options
    jvmOptions = ['--enable-native-access=javafx.graphics']
}

def copyRuntime = tasks.register('copyRuntime', Copy) {
    def javaHome = System.getenv('JAVA_HOME')
    if (!javaHome) {
        javaHome = project.findProperty('org.gradle.java.home')
    }
    if (!javaHome) {
        throw new GradleException('JAVA_HOME is not set. It is required to bundle a runtime for distribution.')
    }
    from(javaHome)
    into(layout.buildDirectory.dir('runtime'))
}

// Create distribution task - puts files in dist folder at project root
def copyDist = tasks.register('copyDist', Copy) {
    dependsOn tasks.named('createExe')
    dependsOn copyRuntime
    from('build/launch4j') {  // Default location for launch4j output
        include '*.exe'
    }
    into 'dist'

    // Also copy from build/libs for config and apps
    from('build/libs') {
        include '*.jar'
        include 'config/**'
        include 'apps/**'
        into '.'
    }

    from(layout.buildDirectory.dir('runtime')) {
        into 'runtime'
    }
}

// Zip the distribution folder
def zipDist = tasks.register('zipDist', Zip) {
    dependsOn copyDist
    from 'dist'
    exclude '*.zip'
    archiveFileName = "acommander-v${appVersion}.zip"
    destinationDirectory = file('dist')
}

// Make dist task run both copy and zip
tasks.register('dist') {
    dependsOn zipDist
    group = 'distribution'
    description = 'Creates distribution folder and ZIP file'
}

// Fix task dependencies
tasks.named('shadowJar') { finalizedBy copyResources }
tasks.named('createExe') {
    dependsOn tasks.named('shadowJar')
    mustRunAfter copyResources
}
tasks.named('startScripts') {
    dependsOn tasks.named('shadowJar')
    dependsOn copyResources
}
